plugins {
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
}

group = 'com.udemy.spring'
version = '0.0.1-SNAPSHOT'
description = 'spring selenium framework'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

ext {
    seleniumVersion = '4.22.0'
    webdrivermanagerVersion = '5.9.1'
    testngVersion = '7.9.0'
    cucumberVersion = '7.14.0'
    fakerVersion = '1.0.2'
    snakeyamlVersion = '2.2'
    lombokVersion = '1.18.22'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2'
    implementation "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    implementation "io.github.bonigarcia:webdrivermanager:$webdrivermanagerVersion"
    implementation("com.github.javafaker:javafaker:$fakerVersion") {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    implementation "org.yaml:snakeyaml:$snakeyamlVersion"
    implementation "org.projectlombok:lombok:$lombokVersion"

    testImplementation "io.cucumber:cucumber-java:$cucumberVersion"
    testImplementation "io.cucumber:cucumber-spring:$cucumberVersion"
    testImplementation "io.cucumber:cucumber-testng:$cucumberVersion"
    testImplementation "org.testng:testng:$testngVersion"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // LOMBOK Dependencies
    implementation "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
}

tasks.register("springTest", Test) {
    group = "verification"
    useTestNG {
        suites("src/test/resources/testng-suite.xml")
    }
}

def final DEFAULT_THREAD_COUNT = 5 as String

tasks.register('cucumberTest', Test) {
    logger.info("Running cucumber tests")
    group = "verification"

    // Load the application.properties file
    def appProperties = new Properties()
    file("src/main/resources/application.properties").withInputStream { appProperties.load(it) }

    // Get the threadCount from the application.properties file
    def threadCountFromProperties = appProperties.getProperty('thread.count', DEFAULT_THREAD_COUNT)
    println "Thread count from properties: $threadCountFromProperties"

    // Get the threadCount from the console
    def threadCountFromConsole = System.getProperty('thread.count', null)
    println "Thread count from console: $threadCountFromConsole"

    def finalThreadCount = threadCountFromConsole ?: threadCountFromProperties
    // Set the 'dataproviderthreadcount' VM option to manage the thread count for the data provider
    systemProperty 'dataproviderthreadcount', finalThreadCount
    println "The final thread count is: " + finalThreadCount

    useTestNG {
        suites("src/test/resources/cucumber-suite.xml")
        // Set the parallel mode to tests with the thread count. Only for the testNG tests, not Cucumber
        parallel = 'tests'
        threadCount = finalThreadCount as int
    }
}
